apply plugin: 'com.android.application'

File keystoreProperties = rootProject.file("keystore.properties")
def props = new Properties()
if (keystoreProperties.exists()) {
    props.load(new FileInputStream(keystoreProperties))
} else {
    props['keystorefile'] = 'none';
}

def buildNumber = System.getenv("BUILD_NUMBER") as Integer ?: Integer.parseInt(VERSION_CODE)

android {

    try {
        compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
    } catch (Exception e) {
        compileSdkVersion project.COMPILE_SDK_VERSION
    }

    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.klinker.android.twitter_l"

        try {
            targetSdkVersion Integer.parseInt(project.TARGET_SDK_VERSION)
        } catch (Exception e) {
            targetSdkVersion project.TARGET_SDK_VERSION
        }

        minSdkVersion Integer.parseInt(project.MIN_SDK)
        versionCode buildNumber
        versionName project.VERSION_NAME

        jackOptions {
            enabled Boolean.parseBoolean(project.USE_JACK)
        }
    }

    File keystore = new File(props['keystorefile'])
    if (keystore.exists()) {
        signingConfigs {
            release {
                storeFile keystore
                storePassword props['keystorepassword']
                keyAlias props['keyalias']
                keyPassword props['keypassword']
            }
            debug {
                storeFile file("../Other/debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }

        buildTypes {
            debug {
                signingConfig signingConfigs.debug
            }
            release {
                minifyEnabled true
                shrinkResources true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), '../app/proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    } else {
        signingConfigs {
            debug {
                storeFile file("../Other/debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }
        buildTypes {
            debug {
                signingConfig signingConfigs.debug
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':shared_assets')
    compile 'com.google.android.support:wearable:1.0.0'
}
