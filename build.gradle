/*
 * Copyright (C) 2016 Source Allies, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

File keystoreProperties = rootProject.file("keystore.properties")
def props = new Properties()
if (keystoreProperties.exists()) {
    props.load(new FileInputStream(keystoreProperties))
} else {
    props['keystorefile'] = 'none'
}

buildscript {

    ext.buildInfo = [
            'id'                : 'com.klinker.android.twitter_l',
            'name'              : '7.5.2',
            'number'            : System.getenv("BUILD_NUMBER") as Integer ?: 1458,
    ]

    ext.versions = [
            'gradlePlugin'      : '3.2.1',
            'targetSdk'         : 28,
            'minSdk'            : 19,

            'kotlin'            : '1.3.10',

            'androidX'          : '1.0.0',
            'playServices'      : '16.0.0',
            'firebaseCore'      : '16.0.4',
    ]

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.gradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.google.gms:google-services:4.1.0'
        classpath 'io.fabric.tools:gradle:1.25.4'
    }

}

allprojects {

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://dl.bintray.com/drummer-aidan/maven" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        flatDir { dirs 'lib' }
    }

}

// add the common project properties, to each app variant
subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {

            android {
                compileSdkVersion versions.targetSdk

                defaultConfig {
                    targetSdkVersion versions.targetSdk
                    minSdkVersion versions.minSdk

                    versionName(buildInfo.name + '.' + buildInfo.number)
                    versionCode buildInfo.number

                    vectorDrawables.useSupportLibrary = true
                    multiDexEnabled true
                }

                File keystore = new File(props['keystorefile'])
                if (keystore.exists()) {
                    signingConfigs {
                        debug {
                            storeFile file("Other/debug.keystore")
                            storePassword "android"
                            keyAlias "androiddebugkey"
                            keyPassword "android"
                        }
                        release {
                            storeFile keystore
                            storePassword props['keystorepassword']
                            keyAlias props['keyalias']
                            keyPassword props['keypassword']
                        }
                    }

                    buildTypes {
                        debug {
                            signingConfig signingConfigs.debug
                        }
                        release {
                            minifyEnabled true
                            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                            signingConfig signingConfigs.release
                        }
                    }
                } else {
                    signingConfigs {
                        debug {
                            storeFile file("Other/debug.keystore")
                            storePassword "android"
                            keyAlias "androiddebugkey"
                            keyPassword "android"
                        }
                    }
                    buildTypes {
                        debug {
                            signingConfig signingConfigs.debug
                        }
                    }
                }

                dexOptions {
                    jumboMode true
                    javaMaxHeapSize "4g"
                }

                packagingOptions {
                    exclude 'LICENSE'
                    exclude 'README.md'
                    exclude 'Other/'
                    exclude 'META-INF/LICENSE'
                    exclude 'META-INF/LICENSE.txt'
                    exclude 'META-INF/NOTICE'
                    exclude 'META-INF/DEPENDENCIES'
                }

                lintOptions {
                    ignore "InnerClasses"
                    abortOnError false
                }

                aaptOptions {
                    noCompress 'apk'
                }

                testOptions {
                    unitTests {
                        includeAndroidResources = true
                    }
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                sourceSets {
                    androidTest {
                        setRoot('src/test')
                    }
                }
            }
        }

    }
}
