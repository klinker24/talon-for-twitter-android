apply plugin: 'com.android.application'

File keystoreProperties = rootProject.file("keystore.properties")
def props = new Properties()
if (keystoreProperties.exists()) {
    props.load(new FileInputStream(keystoreProperties))
} else {
    props['keystorefile'] = 'none';
}

android {
    dexOptions {
        jumboMode true
    }

    packagingOptions {
        exclude 'LICENSE'
        exclude 'README.md'
        exclude 'Other/'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
	    exclude 'META-INF/DEPENDENCIES'
    }

    try {
        compileSdkVersion Integer.parseInt(project.COMPILE_SDK)
    } catch (Exception e) {
        compileSdkVersion project.COMPILE_SDK
    }

    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.klinker.android.twitter_l"

        try {
            targetSdkVersion Integer.parseInt(project.TARGET_SDK)
        } catch (Exception e) {
            targetSdkVersion project.TARGET_SDK
        }

        minSdkVersion Integer.parseInt(project.MIN_SDK)
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME

        jackOptions {
            enabled Boolean.parseBoolean(project.USE_JACK)
        }
    }

    File keystore = new File(props['keystorefile'])
    if (keystore.exists()) {
        signingConfigs {
            release {
                storeFile keystore
                storePassword props['keystorepassword']
                keyAlias props['keyalias']
                keyPassword props['keypassword']
            }
            debug {
	            storeFile file("../Other/debug.keystore")
	            storePassword "android"
	            keyAlias "androiddebugkey"
	            keyPassword "android"
	        }
        }

        buildTypes {
            debug {
                signingConfig signingConfigs.debug
            }
            release {
                minifyEnabled true
                shrinkResources true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    } else {
		signingConfigs {
            debug {
                storeFile file("../Other/debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }
        buildTypes {
            debug {
                signingConfig signingConfigs.debug
            }
        }
    }

    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        noCompress 'apk'
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {

    wearApp project(':wear')
    
    provided 'org.projectlombok:lombok:1.14.8'

    compile "com.android.support:appcompat-v7:${project.ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:cardview-v7:${project.ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:design:${project.ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:customtabs:${project.ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:preference-v7:${project.ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:preference-v14:${project.ANDROID_SUPPORT_VERSION}"

    compile 'com.klinkerapps:peekview:1.2.2'
    compile 'com.klinkerapps:simple_videoview:1.2.4'
    compile 'com.klinkerapps:launcher-apis:1.1.0'
    compile 'com.klinkerapps:sliding-activity:1.4.3'
    compile 'com.klinkerapps:android-transcoder:1.0.1'
    compile 'com.klinkerapps:article_viewer:0.17.2'
    compile 'com.klinkerapps:drag-dismiss-activity:0.0.4-SNAPSHOT'

    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.afollestad:material-camera:0.4.4'
    compile 'com.afollestad:easyvideoplayer:0.3.0'
    compile 'com.github.paolorotolo:appintro:3.1.0'
    compile 'de.timroes.android:EnhancedListView:0.3.2@aar'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.flipboard:bottomsheet-core:1.5.0'
    compile 'com.yalantis:ucrop:1.5.0'
    compile 'me.leolin:ShortcutBadger:1.1.6@aar'
    compile 'com.github.ajalt.reprint:core:2.9.2@aar'

    //compile 'org.twitter4j:twitter4j-core:4.0.4'
    //compile 'org.twitter4j:twitter4j-stream:4.0.4'
    //compile 'org.twitter4j:twitter4j-http2-support:4.0.4'

    compile 'org.apache.httpcomponents:httpmime:4.3.5'
    compile 'org.apache.httpcomponents:httpcore:4.3.2'
    compile 'org.jsoup:jsoup:1.7.3'

    compile files('lib/YouTubeAndroidPlayerApi.jar') // version 1.2.2
    compile files('lib/twitter4j-fork.jar')
    compile files('lib/twitter4j-stream-fork.jar')
    compile(name: 'datetimepicker', ext: 'aar')
    compile(name: 'sectioned-recyclerview', ext: 'aar')

    compile project(':searchview')
    compile project(':lvl')
    compile project(':shared_assets')
}
